(* Examples on streams. *)

extern not : `(ω) ⊛ (bool → bool)

extern tail : `(ω) ⊛ (stream bool → `0(1) ⊛ stream bool)

rec
{
  const (x : int) : stream int =
    x :: (const x) by `0(1);

  map (f : int → int) ((x :: xs) : stream int) : stream int =
    f x :: (map f xs) by `0(1);

  zipWith
    (f : int → int → int)
    ((x :: xs) : stream int)
    ((y :: ys) : stream int) : stream int =
    (f x y) :: (zipWith f xs ys) by `0(1);
}

{
  plus (x : int) (y : int) : int = x + y;
}

{
  zero = const 0;

  incr = map (λ (x : int) ⇒ x + 1);

  one = incr zero;
}

rec
{
  nat : `(1 0) ⊛ stream int = (0 :: pos) by `(1 0);

  pos : `(0 1) ⊛ stream int = (incr nat) by `(0 1);
}

rec
{
  nat : stream int =
    0 :: (map (λ (x : int) ⇒ x + 1) nat) by `0(1);
}

rec
{
  h ((x :: xs) : stream bool) : `(2) ⊛ stream bool =
    let { ys = (h xs) by `0(1) } in
    (x :: (not x :: ys) by `0(1)) by `(2);
}

rec
{
  thuemorse : stream bool =
    let { ys = (h thuemorse) by `0(1) } in
    false :: (tail ys) by `0(2);
}

extern tail : `(ω) ⊛ (stream int → `0(1) ⊛ stream int)

rec
{
  fib : stream int =
    let { ys = (tail fib) by `0(1) } in
    0 :: (1 :: (zipWith plus fib ys) by `0(1)) by `0(1);
}

{
  iter (f : int -> int) (ini : int) : stream int =
    loop ini where rec
    { loop (acc : int)
      : stream int
      = acc :: (loop (f acc)) by `0(1) }
}

(* Generic but useless fixpoint combinator. *)

{
  zeroFix (f : stream int → stream int) =
    let rec { x : `(0) ⊛ stream int = (f x) by `(0); } in
    x
}

(* Isomorphism between later(X → Y) and later(X) → later(Y). *)
{
  isoL (f : `0(1) ⊛ (stream int → stream int)) (x : `0(1) ⊛ stream int) =
    (f x) by `0(1);

  isoR (f : `0(1) ⊛ stream int → `0(1) ⊛ stream int) =
    (λ (x : stream int) ⇒ (f x) by `2(1)) by `0(1);
}
